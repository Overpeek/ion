use std::{str::FromStr, borrow::Cow};
use crate::ast::*;

//

grammar<'err>(errors: &'err mut Vec<String>);

extern {
    type Error = String;
}

//

pub Module: Module<'input> = {
    Stmts => Module::from(<>),
};

Stmts: Stmts<'input> = {
    <s: (<Stmt> ";")*> => Stmts(s),
};

pub Stmt: Stmt<'input> = {
    Expr => Stmt::Expr(<>),
    <i:Ident> "=" <e:Expr> => Stmt::Assign(Assign::new(i, e)),
    "global" <i:Ident> "=" <e:Expr> => Stmt::Assign(Assign::new_global(i, e)),
};

Expr: Expr<'input> = {
    // Function declaration
    "fn(" <p:Parameters> ")" <b:Block> => {
        Expr::NamelessFunc(
            Func::new()
                .with_params(p)
                .with_random_name()
                .with_block(b)
        )
    },

    // Function call
    <i:Ident> <t:Tuple> => Expr::FuncCall(FuncCall::new(i, t)),

    // Binary expressions
    BinExpr => <>,
};

Block: Block<'input> = {
    "{" <s: Stmts> "}" => Block(s),
};

Tuple: Vec<Expr<'input>> = {
    "(" <v:Comma<Expr>> ")" => v,
};

Parameters = Comma<Ident>;

Comma<T>: Vec<T> = {
    <mut v: (<T> ",")*> <e:T?> => match e {
        Some(e) => {
            v.push(e);
            v
        },
        None => v,
    }
};

Ident: Ident<'input> = {
    r"[a-zA-Z_][\w_]*" => Cow::Borrowed(<>),
};

BinExpr: Expr<'input> = {
    <l:BinExpr> <op:BinOp> <r:Term> => Expr::BinExpr(BinExpr::new(l, op, r)),
    Term => <>,
};

BinOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
};

Term: Expr<'input> = {
    <l:Term> <op:TermOp> <r:Fact> => Expr::BinExpr(BinExpr::new(l, op, r)),
    Fact => <>,
};

TermOp: BinOp = {
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
};

Fact: Expr<'input> = {
    "(" <e:Expr> ")" => e,
    Path => Expr::Path(<>),
    Literal => Expr::Literal(<>),
};

Path: Path<'input> = {
    <mut p: (<Ident> ".")*> <i:Ident> => {
        p.push(i);
        Path::new(p)
    },
};

Literal: Literal<'input> = {
    r#"\d[\d_]+\.\d[\d_]+"# =>? Literal::parse_float(<>),
    r"\d[\d_]*" =>? Literal::parse_int(<>),
    r#""[^"]*""# => Literal::parse_str(<>),
    "true" => Literal::Bool(true),
    "false" => Literal::Bool(false),
};
